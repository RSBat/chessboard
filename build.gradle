import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    dependencies {
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:1.7.10")
    }
}

plugins {
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id "org.jetbrains.dokka" version "1.7.10" apply false
    id "io.gitlab.arturbosch.detekt" version "1.21.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Detekt report merging
tasks.register("reportMerge", ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml") // or "reports/detekt/merge.sarif"
}

subprojects {
    plugins.withType(DetektPlugin) {
        tasks.withType(Detekt) { detektTask -> // Sadly it has to be eager.
            finalizedBy(reportMerge)

            reportMerge.configure { mergeTask ->
                mergeTask.input.from(detektTask.sarifReportFile)
            }
        }
    }
}
// END Detekt report merging